------------------------------- MODULE Synod -------------------------------

EXTENDS Naturals

CONSTANTS N , Inputs
ASSUME (N \in Nat) /\ (N > 0)

Proc == 1..N

NotAnInput == CHOOSE c : c \notin Inputs

VARIABLES input, output

------------------------------- MODULE Inner -------------------------------

VARIABLES allInput, chosen

IInit == /\ input \in [Proc -> Inputs]
         /\ output = [p \in Proc |-> NotAnInput]
         /\ chosen = NotAnInput
         /\ allInput = {input[p] : p \in Proc}

IChoose(p) ==
  /\ output[p] = NotAnInput
  /\ IF chosen = NotAnInput
     THEN \E ip \in allInput : /\ chosen' = ip
                               /\ output' = [output EXCEPT ![p] = ip]
     ELSE /\ output' = [output EXCEPT ![p] = chosen]
          /\ UNCHANGED chosen
  /\ UNCHANGED <<input, allInput>>

IFail(p) ==
  /\ output' = [output EXCEPT ![p] = NotAnInput]
  /\ \E ip \in Inputs : /\ input' = [input EXCEPT ![p] = ip]
                        /\ allInput = allInput \cup {ip}
  /\ UNCHANGED chosen

INext == \E p \in Proc : IChoose(p) \/ IFail (p)

ISpec == IInit /\ [][INext]_<<input, output, chosen, allInput>>

=============================================================================

IS(chosen, allInput) == INSTANCE Inner

SynodSpec == \EE chosen, allInput : IS(chosen, allInput)!ISpec



=============================================================================


----------------------------- MODULE AsdfSynod -----------------------------

EXTENDS Synod, FiniteSets

CONSTANTS Ballot(_), Asdf, IsMajority(_)
ASSUME /\ \A p \in Proc : /\ Ballot(p) \subseteq {n \in Nat : n > 0}
                          /\ \A q \in Proc \ {p} : Ballot(p) \cap Ballot(q) = {}
       /\ IsMajority(Asdf)
       /\ \A S, T \in SUBSET Asdf :
              IsMajority(S) /\ IsMajority(T) => (S \cup T # {})

AsdfBlock == [mbal : (UNION {Ballot(p) : p \in Proc}) \cup {0},
              bal  : (UNION {Ballot(p) : p \in Proc}) \cup {0},
              inp  : Inputs \cup {NotAnInput}]

InitDB == [mbal |-> 0, bal |-> 0, inp |-> NotAnInput]

VARIABLES asdf , dblock , phase, asdfsWritten, blocksRead

vars == <<input, output, asdf , phase, dblock , asdfsWritten, blocksRead>>

Init == /\ input \in [Proc -> Inputs]
        /\ output = [p \in Proc |-> NotAnInput]
        /\ asdf = [d \in Asdf |-> [p \in Proc |-> InitDB ]]
        /\ phase = [p \in Proc |-> 0]
        /\ dblock = [p \in Proc |-> InitDB ]
        /\ asdfsWritten = [p \in Proc |-> {}]
        /\ blocksRead = [p \in Proc |-> [d \in Asdf |-> {}]]
       
hasRead (p, d , q) == \E br \in blocksRead [p][d ] : br .proc = q

allBlocksRead(p) ==
  LET allRdBlks == UNION {blocksRead [p][d ] : d \in Asdf }
  IN {br.block : br \in allRdBlks}

InitializePhase(p) ==
  /\ asdfsWritten' = [asdfsWritten EXCEPT ![p] = {}]
  /\ blocksRead' = [blocksRead EXCEPT ![p] = [d \in Asdf |-> {}]]

StartBallot(p) ==
  /\ phase[p] \in {1, 2}
  /\ phase' = [phase EXCEPT ![p] = 1]
  /\ \E b \in Ballot(p) : /\ b > dblock [p].mbal
                          /\ dblock' = [dblock EXCEPT ![p].mbal = b]
  /\ InitializePhase(p)
  /\ UNCHANGED <<input, output, asdf>>

Phase1or2Write(p, d) ==
  /\ phase[p] \in {1, 2}
  /\ asdf' = [asdf EXCEPT ![d][p] = dblock[p]]
  /\ asdfsWritten' = [asdfsWritten EXCEPT ![p] = @ \cup {d}]
  /\ UNCHANGED <<input, output, phase, dblock , blocksRead>>

Phase1or2Read (p, d, q) ==
  /\ d \in asdfsWritten[p]
  /\ IF asdf [d ][q].mbal < dblock [p].mbal
     THEN /\ blocksRead' = 
                [blocksRead EXCEPT
                    ![p][d ] = @ \cup {[block |-> asdf [d ][q], proc |-> q]}]
          /\ UNCHANGED <<input, output, asdf , phase, dblock , asdfsWritten>>
     ELSE StartBallot(p)

EndPhase1or2(p) ==
  /\ IsMajority({d \in asdfsWritten[p] :
                    \A q \in Proc \ {p} : hasRead (p, d , q)})
  /\ \/ /\ phase[p] = 1
        /\ dblock' =
              [dblock EXCEPT
                ![p].bal = dblock [p].mbal,
                ![p].inp =
                    LET blocksSeen == allBlocksRead(p) \cup {dblock [p]}
                        nonInitBlks ==
                            {bs \in blocksSeen : bs.inp # NotAnInput}
                        maxBlk ==
                            CHOOSE b \in nonInitBlks :
                                \A c \in nonInitBlks : b.bal >= c.bal
                    IN IF nonInitBlks = {} THEN input[p]
                       ELSE maxBlk .inp ]
        /\ UNCHANGED output
     \/ /\ phase[p] = 2
        /\ output' = [output EXCEPT ![p] = dblock [p].inp]
        /\ UNCHANGED dblock
  /\ phase' = [phase EXCEPT ![p] = @ + 1]
  /\ InitializePhase(p)
  /\ UNCHANGED <<input, asdf>>

Fail(p) ==
  /\ \E ip \in Inputs : input' = [input EXCEPT ![p] = ip]
  /\ phase' = [phase EXCEPT ![p] = 0]
  /\ dblock' = [dblock EXCEPT ![p] = InitDB]
  /\ output' = [output EXCEPT ![p] = NotAnInput]
  /\ InitializePhase(p)
  /\ UNCHANGED asdf

Phase0Read(p, d) ==
  /\ phase[p] = 0
  /\ blocksRead' = [blocksRead EXCEPT
                        ![p][d ] = @ \cup {[block |-> asdf [d][p], proc |-> p]}]
  /\ UNCHANGED <<input, output, asdf , phase, dblock , asdfsWritten>>
  

EndPhase0(p) ==
  /\ phase[p] = 0
  /\ IsMajority({d \in Asdf : hasRead (p, d, p)})
  /\ \E b \in Ballot(p) :
        /\ \A r \in allBlocksRead (p) : b > r.mbal
        /\ dblock' = [dblock EXCEPT
                        ![p] = [(CHOOSE r \in allBlocksRead (p) :
                                     \A s \in allBlocksRead (p) : r .bal >= s.bal)
                                  EXCEPT !.mbal = b]]
  /\ InitializePhase(p)
  /\ phase' = [phase EXCEPT ![p] = 1]
  /\ UNCHANGED <<input, output, asdf>>

Next == \E p \in Proc :
          \/ StartBallot(p)
          \/ \E d \in Asdf : \/ Phase0Read (p, d)
                             \/ Phase1or2Write(p, d)
                             \/ \E q \in Proc \ {p} : Phase1or2Read (p, d, q)
          \/ EndPhase1or2(p)
          \/ Fail(p)
          \/ EndPhase0(p)

AsdfSynodSpec == Init /\ [][Next]_vars

THEOREM AsdfSynodSpec => SynodSpec


=============================================================================


----------------------------- MODULE HAsdfSynod -----------------------------

EXTENDS AsdfSynod
VARIABLES allInput, chosen

HInit == /\ Init
         /\ chosen = NotAnInput
         /\ allInput = {input[p] : p \in Proc}
        
HNext == /\ Next
         /\ chosen' = LET hasOutput(p) == output'[p] # NotAnInput
                      IN IF \/ chosen # NotAnInput
                            \/ \A p \in Proc : ~hasOutput(p)
                         THEN chosen
                         ELSE output'[CHOOSE p \in Proc : hasOutput(p)]
         /\ allInput' = allInput \cup {input'[p] : p \in Proc}        
        

HInv1 ==
  /\ input \in [Proc -> Inputs]
  /\ output \in [Proc -> Inputs \cup {NotAnInput}]
  /\ asdf \in [Asdf -> [Proc -> AsdfBlock ]]
  /\ phase \in [Proc -> 0..3]
  /\ dblock \in [Proc -> AsdfBlock ]
  /\ output \in [Proc -> Inputs \cup {NotAnInput}]
  /\ asdfsWritten \in [Proc -> SUBSET Asdf ]
  /\ blocksRead \in [Proc -> [Asdf -> SUBSET [block : AsdfBlock , proc : Proc]]]
  /\ allInput \in SUBSET Inputs
  /\ chosen \in Inputs \cup {NotAnInput}
  
MajoritySet == {D \in SUBSET Asdf : IsMajority(D)}

blocksOf(p) ==
  LET rdBy(q, d) == {br \in blocksRead[q][d] : br.proc = p}
  IN {dblock[p]} \cup {asdf[d][p] : d \in Asdf }
        \cup {br.block : br \in UNION{rdBy(q, d) : q \in Proc, d \in Asdf}}

allBlocks == UNION {blocksOf(p) : p \in Proc}  

HInv2 ==
  /\ \A p \in Proc :
      \A bk \in blocksOf (p) : /\ bk.mbal \in Ballot(p) \cup {0}
                               /\ bk.bal \in Ballot(p) \cup {0}
                               (*/\ (bk.bal = 0) â‰¡ (bk.inp = NotAnInput)*)
                               /\ (bk.bal = 0) <=> (bk.inp = NotAnInput)
                               /\ bk.mbal >= bk.bal
                               /\ bk.inp \in allInput \cup {NotAnInput}
  /\ \A p \in Proc, d \in Asdf :
        /\ (d \in asdfsWritten[p]) => /\ phase[p] \in {1, 2}
                                      /\ asdf[d][p] = dblock[p]
        /\ (phase[p] \in {1, 2}) => /\ (blocksRead [p][d ] # {}) =>
                                            (d \in asdfsWritten[p])
                                    /\ ~hasRead (p, d, p)
  /\ \A p \in Proc :
        /\ (phase[p] = 0) => /\ dblock[p] = InitDB
                             /\ asdfsWritten[p] = {}
                             /\ \A d \in Asdf : \A br \in blocksRead[p][d] :
                                      /\ br.proc = p
                                      /\ br.block = asdf [d ][p]
        /\ (phase[p] # 0) => /\ dblock[p].mbal \in Ballot(p)
                             /\ dblock[p].bal \in Ballot(p) \cup {0}
                             /\ \A d \in Asdf : \A br \in blocksRead [p][d ] :
                                          br.block.mbal < dblock[p].mbal
        /\ (phase[p] \in {2, 3}) => (dblock[p].bal = dblock[p].mbal)
        /\ output[p] = IF phase[p] = 3 THEN dblock[p].inp ELSE NotAnInput
  /\ chosen \in allInput \cup {NotAnInput}
  /\ \A p \in Proc : /\ input[p] \in allInput
                     /\ (chosen = NotAnInput) => (output[p] = NotAnInput)

HInv3 == \A p, q \in Proc, d \in Asdf :
              /\ phase[p] \in {1, 2}
              /\ phase[q] \in {1, 2}
              /\ hasRead (p, d, q)
              /\ hasRead (q, d, p)
            => \/ [block |-> dblock [q], proc |-> q] \in blocksRead[p][d]
               \/ [block |-> dblock [p], proc |-> p] \in blocksRead[q][d]

HInv4 ==
  \A p \in Proc :
      /\ (phase[p] # 0) =>
            /\ \A bk \in blocksOf(p) : dblock[p].mbal >= bk.bal
            /\ \A D \in MajoritySet :
                    \E d \in D : /\ dblock[p].mbal >= asdf[d][p].mbal
                                 /\ dblock[p].bal >= asdf[d][p].bal
      /\ (phase[p] = 1) => (\A bk \in blocksOf(p) : dblock[p].mbal > bk.bal)
      /\ (phase[p] \in {2, 3}) =>
            (\E D \in MajoritySet : \A d \in D : asdf[d][p].mbal = dblock[p].bal)
      /\ \A bk \in blocksOf(p) :
            \E D \in MajoritySet : \A d \in D : asdf[d][p].mbal >= bk.bal

maxBalInp(b, v ) == \A bk \in allBlocks : (bk.bal >= b) => (bk.inp = v)

HInv5 ==
  \A p \in Proc :
    (phase[p] = 2) => \/ maxBalInp(dblock[p].bal , dblock[p].inp)
                      \/  \E D \in MajoritySet, q \in Proc :
                              \A d \in D : /\ asdf[d][q].mbal > dblock [p].bal
                                           /\ ~hasRead (p, d , q)

valueChosen(v) ==
  \E b \in UNION {Ballot(p) : p \in Proc} :
      /\ maxBalInp(b, v)
      /\ \E p \in Proc, D \in MajoritySet :
              \A d \in D : /\ asdf[d][p].bal >= b
                           /\ \A q \in Proc :
                                  /\ phase[q] = 1
                                  /\ dblock[q].mbal >= b
                                  /\ hasRead(q, d, p)
                                => (\E br \in blocksRead[q][d] : br.block.bal >= b)

HInv6 == /\ (chosen # NotAnInput) => valueChosen(chosen)
         /\ \A p \in Proc : output[p] \in {chosen, NotAnInput}


=============================================================================
